group 'no.greenall'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'cpd'
apply plugin: 'gradle-one-jar'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

configurations {
    // We're logging with slf4j api and log4j2 backend so all other backends must be removed
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'org.slf4j', module: 'slf4j-nop'
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'commons-logging'

}

def version_jetty = '9.3.8.v20160314'
def version_slf4j = '1.7.19'
def version_jersey = '2.22.2'
dependencies {
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: version_jetty
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: version_jetty
    compile group: 'org.slf4j', name: 'slf4j-api', version: version_slf4j
    compile group: 'org.slf4j', name: 'slf4j-simple', version: version_slf4j
    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: version_jersey
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version: version_jersey

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.glassfish.jersey.test-framework', name: 'jersey-test-framework-util', version: version_jersey

}

task oneJar(type: OneJar) {
    mainClass = 'no.greenall.jouren.App'
}

checkstyle {
    configProperties = [samedir: "${rootDir}"]
    // Cannot go higher as 6.8 breaks compatibility with the current Gradle checkstyle plugin
    // See change to CheckStyleTask here: http://checkstyle.sourceforge.net/releasenotes.html#Release_6.8
    toolVersion = 6.7
}

pmd {
    consoleOutput = true
    targetJdk = 1.7 // 1.8 is yet unsupported
    toolVersion = '5.3.3'
}

cpd {
    toolVersion = '5.3.3'
    ignoreIdentifiers = true
    ignoreLiterals = true

    ignoreFailures = true // not failing the build for now
}

cpdCheck {
    reports {
        text.enabled = true
        xml.enabled = false
    }
}

buildscript {
    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath 'de.aaschmid.gradle.plugins:gradle-cpd-plugin:0.5'
    }
    repositories {
        mavenCentral()
        maven {
            url '~/.m2/repository'
        }
        jcenter()
    }
}